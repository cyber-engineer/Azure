//Hunt for newly added users (past 30 days) that were added to an Azure AD role soon after its creation.


// New Azure AD users added to Azure AD roles
  let NewUsers = AuditLogs
      | where TimeGenerated > ago(30d)
      | where ActivityDisplayName == "Add user"
      | extend UserID = tostring(TargetResources[0].id);
  let NewAdmins = AuditLogs
      | where TimeGenerated > ago(30d)
      | where ActivityDisplayName == "Add member to role"
      | extend UserID = tostring(TargetResources[0].id);
  NewUsers
      | join (NewAdmins) on UserID
      | project UserCreatedTimestamp=TimeGenerated, UserPrincipalName=tostring(TargetResources[0].userPrincipalName), UserID, AdminRoleAddedTimestamp=TimeGenerated1, AdminRole=tostring(TargetResources1[0].modifiedProperties[1].newValue), InitiatedBy=tostring(InitiatedBy1.user.userPrincipalName)
