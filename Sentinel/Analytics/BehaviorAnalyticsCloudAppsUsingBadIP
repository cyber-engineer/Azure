//Hunt for threat intelligence matches in the Sentinel UEBA table and Defender for Cloud Apps.
//This will indicate if any cloud app is accessed from an IP-address associated with a, by Microsoft, known threat.

// Recent cloud app activity originating from, by Microsoft, known bad IP addresses.
  let BadIPAddresses = BehaviorAnalytics
    | where TimeGenerated > ago(30d)
    | extend ThreatIntelIndicatorDescription = DevicesInsights.ThreatIntelIndicatorDescription
    | where isnotempty( ThreatIntelIndicatorDescription )
    | project SourceIPAddress, ThreatIntelIndicatorDescription;
  CloudAppEvents
    | where TimeGenerated > ago(30d)
      | join kind=inner (
          BadIPAddresses
      ) on $left.IPAddress == $right.SourceIPAddress
      | summarize LastActivity=max(TimeGenerated), count() by AccountDisplayName, Application, IPAddress, ISP, CountryCode, tostring(ThreatIntelIndicatorDescription)
      | order by IPAddress, AccountDisplayName

